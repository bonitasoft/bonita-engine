import org.bonitasoft.engine.gradle.PomUtils

plugins {
    id "java-library"
    id "bonita-tests"
}

dependencies {
    api project(':bpm:bonita-common')
    api libs.httpComponentsClient
    api libs.xstream
    api libs.httpComponentsMime
    testImplementation libs.junit4
    testImplementation libs.assertj
    testImplementation libs.logback
    testImplementation libs.mockitoCore
    testImplementation libs.jettyServer
    testImplementation libs.jettySecurity
}

tasks.register("testsJar", Jar) {
    archiveClassifier = 'tests'
    from(sourceSets.test.output)
}

tasks.register("sourcesJar", Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

tasks.register("javadocJar", Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from project.components.java
            artifact project.sourcesJar
            artifact project.javadocJar
            artifact project.testsJar
            pom { pom ->
                name = "Bonita Client"
                description = "Bonita Client is the Jar used to interact with a running Bonita Engine"
                PomUtils.pomCommunityPublication(pom)
            }
        }
    }
}
