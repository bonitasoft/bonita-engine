configurations { schemagen }
dependencies {
    api project(':bpm:bonita-common')
    api project(':bpm:bonita-core:bonita-process-definition')
    api project(':services:bonita-identity')
    api project(':services:bonita-persistence')
    api project(':services:bonita-commons')
    api project(':services:bonita-log')
    api project(':services:bonita-events')
    api project(':services:bonita-archive')
    api project(':services:bonita-data-instance')
    api project(':services:bonita-classloader')
    api project(':bpm:bonita-core:bonita-process-comment')
    api project(':services:bonita-builder')
    api project(':bpm:bonita-core:bonita-home-server')
    api project(':services:bonita-session')
    api project(':services:bonita-connector-executor')
    api project(':services:bonita-resources')
    api project(':services:bonita-time-tracker')
    api project(':services:bonita-expression')
    api project(':services:bonita-cache')
    api project(':bpm:bonita-core:bonita-contract-data')
    testImplementation "junit:junit:${Deps.junit4Version}"
    testImplementation "org.assertj:assertj-core:${Deps.assertjVersion}"
    testImplementation "org.mockito:mockito-core:${Deps.mockitoVersion}"
    testImplementation "com.github.stefanbirkner:system-rules:${Deps.systemRulesVersion}"
    testRuntimeOnly "ch.qos.logback:logback-classic:${Deps.logbackVersion}"

    annotationProcessor "org.projectlombok:lombok:${Deps.lombokVersion}"
    compileOnly "org.projectlombok:lombok:${Deps.lombokVersion}"

    schemagen(
            "com.sun.xml.bind:jaxb-xjc:${Deps.jaxbVersion}",
            "org.glassfish.jaxb:jaxb-runtime:${Deps.jaxbVersion}",
            "javax.xml.bind:jaxb-api:${Deps.jaxbVersion}",
            "org.glassfish.jaxb:jaxb-jxc:${Deps.jaxbVersion}",
            "com.sun.activation:jakarta.activation:${Deps.activationVersion}")
}

task('schemagen') {
    def destDir = file("$buildDir/schemas")
    doLast {
        destDir.mkdirs()
        ant.taskdef(name: 'schemagen', classname: 'com.sun.tools.jxc.SchemaGenTask', classpath: configurations.schemagen.asPath)
        try {
            ant.schemagen(srcdir: new File('src/main/java/'), destdir: destDir, includeAntRuntime: false) {
                classpath { pathelement(path: configurations.schemagen.asPath) }
                include(name: 'org/bonitasoft/engine/core/connector/parser/*.java')
            }
        } catch (Exception e) {
            def schema = file("$destDir/schema1.xsd")
            if (!schema.exists()) throw e
            schema.renameTo(file("$destDir/connectors-impl.xsd"))
        }
    }
    inputs.dir('src/main/java/')
    outputs.file("$destDir/connectors-impl.xsd")
}

processResources { from "$buildDir/schemas" }

tasks.processResources.dependsOn tasks.schemagen

tasks.schemagen.outputs.cacheIf { true }
