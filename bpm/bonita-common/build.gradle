plugins {
    id 'com.intershop.gradle.jaxb' version '2.0.0'
}


dependencies {
    compile "commons-io:commons-io:${Deps.commonsIOVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind:${Deps.jacksonDataBindingVersion}"
    compile "org.slf4j:slf4j-api:${Deps.slf4jVersion}"
    compile "org.apache.commons:commons-lang3:${Deps.commonsLangVersion}"
    compile "commons-codec:commons-codec:${Deps.commonsCodecVersion}"
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.0'
    runtime "javax.xml.bind:jaxb-api:${Deps.jaxbVersion}"
    runtime "com.sun.xml.bind:jaxb-impl:${Deps.jaxbVersion}"
    runtime "javax.activation:activation:${Deps.javaxActivationVersion}"
    testCompile "junit:junit:${Deps.junit4Version}"
    testCompile "org.assertj:assertj-core:${Deps.assertjVersion}"
    testCompile "org.mockito:mockito-core:${Deps.mockitoVersion}"
    testCompile "net.javacrumbs.json-unit:json-unit-fluent:${Deps.jsonUnitVersion}"
    testCompile "xmlunit:xmlunit:${Deps.xmlunitVersion}"
    testCompile "ch.qos.logback:logback-classic:${Deps.logbackVersion}"
    annotationProcessor "org.projectlombok:lombok:${Deps.lombokVersion}"
    compileOnly "org.projectlombok:lombok:${Deps.lombokVersion}"
}

def generateInfoProperties = task("generateInfoProperties") {
    doFirst{
        project.file("build/generated/main/resources").mkdirs()
        project.file("build/generated/main/resources/info.properties").text = "version $project.version"
    }
    inputs.property "version", project.version
    outputs.file project.file("build/generated/main/resources/info.properties")
}
tasks.processResources.dependsOn generateInfoProperties

sourceSets {
    main {
        resources {
            srcDir project.file("build/generated/main/resources")
        }
    }
}
task testsJar(type: Jar) {
    classifier = 'tests'
    from(sourceSets.test.output)
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            from project.components.java
            artifact project.sourcesJar
            artifact project.javadocJar
            artifact project.testsJar
        }
    }
}


jaxb {
    //generates schema from java code
    schemaGen {
        forms {
            javaFiles = fileTree(dir: 'src/main/java', includes: [
                    'org/bonitasoft/engine/bpm/bar/form/model/*.java',
                    'org/bonitasoft/engine/form/*.java'])
            namespaceconfigs = ['http://www.bonitasoft.org/ns/form/6.0': 'form-mapping.xsd']
            outputDir = project.file("build/generated/jaxb/schemas")
        }
        application {
            javaFiles = fileTree(dir: 'src/main/java', includes: [
                    'org/bonitasoft/engine/business/application/xml/**/*.java'],
                    excludes:['**/*Builder.java'])
            namespaceconfigs = ['http://documentation.bonitasoft.com/application-xml-schema/1.0': 'application.xsd']
            outputDir = project.file("build/generated/jaxb/schemas")
        }
        bom {
            javaFiles = fileTree(dir: 'src/main/java', include: 'org/bonitasoft/engine/bdm/model/**/*.java')
            namespaceconfigs = ['': 'bom.xsd']
            outputDir = project.file("build/generated/jaxb/schemas")
        }
        profile {
            javaFiles = fileTree(dir: 'src/main/java', include: 'org/bonitasoft/engine/profile/xml/**/*.java')
            namespaceconfigs = ['http://www.bonitasoft.org/ns/profile/6.1': 'profiles.xsd']
            outputDir = project.file("build/generated/jaxb/schemas")
        }
    }
}

processResources {
    from "$buildDir/generated/jaxb/schemas"
}

//tasks.processResources.dependsOn tasks.jaxbSchemaGenProcessdefinition
tasks.processResources.dependsOn tasks.jaxbSchemaGenApplication
tasks.processResources.dependsOn tasks.jaxbSchemaGenBom
tasks.processResources.dependsOn tasks.jaxbSchemaGenProfile
tasks.processResources.dependsOn tasks.jaxbSchemaGenForms
