import org.bonitasoft.engine.gradle.PomUtils

apply plugin: 'java-test-fixtures'

configurations { schemagen }

dependencies {
    api platform("com.fasterxml.jackson:jackson-bom:${Deps.jacksonBomVersion}")
    api platform("org.bonitasoft.engine:bonita-artifacts-model-dependencies:${Deps.bonitaArtifactsModelVersion}")

    api "org.bonitasoft.engine:bonita-business-archive"
    api "org.bonitasoft.engine:bonita-business-object-model"
    api project(':bpm:bonita-profile-model')
    api "commons-io:commons-io:${Deps.commonsIOVersion}"
    api "com.fasterxml.jackson.core:jackson-databind"
    api "org.slf4j:slf4j-api:${Deps.slf4jVersion}"
    api "org.apache.commons:commons-lang3:${Deps.commonsLangVersion}"
    api "com.fasterxml.jackson.core:jackson-annotations"
    api("javax.xml.bind:jaxb-api") {
        exclude(group: "javax.activation") //replaced by jakarta
    }
    api("org.glassfish.jaxb:jaxb-runtime") {
        exclude(group: "javax.activation") //replaced by jakarta
    }

    runtimeOnly "com.sun.activation:jakarta.activation"

    compileOnly project(':bpm:bonita-sap-jco-connector-api')
    compileOnly "org.projectlombok:lombok:${Deps.lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${Deps.lombokVersion}"

    testImplementation project(':bpm:bonita-sap-jco-connector-api')
    testImplementation "junit:junit:${Deps.junit4Version}"
    testImplementation "org.assertj:assertj-core:${Deps.assertjVersion}"
    testImplementation "org.mockito:mockito-core:${Deps.mockitoVersion}"
    testImplementation "ch.qos.logback:logback-classic:${Deps.logbackVersion}"
    testImplementation 'org.jmockit:jmockit:1.44'
    testImplementation "net.javacrumbs.json-unit:json-unit-fluent:${Deps.jsonUnitVersion}"
    testImplementation "xmlunit:xmlunit:${Deps.xmlunitVersion}"

    testFixturesApi("org.bonitasoft.engine:bonita-business-object-model") {
        artifact { classifier = 'tests' }
    }
    testFixturesImplementation "org.assertj:assertj-core:${Deps.assertjVersion}"

    schemagen("org.glassfish.jaxb:jaxb-jxc:${Deps.jaxbJxcVersion}",
            "org.apache.commons:commons-lang3:${Deps.commonsLangVersion}",
            "org.slf4j:slf4j-api:${Deps.slf4jVersion}")
}

tasks.register("testsJar", Jar) {
    archiveClassifier = 'tests'
    from(sourceSets.test.output)
}

tasks.register("sourcesJar", Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

tasks.register("javadocJar", Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

// Disable publishing of test fixtures variants because their dependencies are published as optional dependencies
// It prevents consumers (e.g. bonita-distrib) to retrieve some transitive dependencies (e.g. bonita-business-object-model)
components.java.withVariantsFromConfiguration(configurations.testFixturesApiElements) { skip() }
components.java.withVariantsFromConfiguration(configurations.testFixturesRuntimeElements) { skip() }

publishing {
    publications {
        mavenJava(MavenPublication) {
            from project.components.java
            artifact project.sourcesJar
            artifact project.javadocJar
            artifact project.testsJar
            pom { pom ->
                name = "Bonita Common"
                description = "Bonita Common is the useful layer common to bonita-client and bonita-server"
                PomUtils.pomCommunityPublication(pom)
            }
        }
    }
}

def schemagen = tasks.register('schemagen') {
    def destDir = file("$buildDir/schemas")
    doLast {
        destDir.mkdirs()
        ant.taskdef(name: 'schemagen', classname: 'com.sun.tools.jxc.SchemaGenTask', classpath: configurations.schemagen.asPath)

        ant.schemagen(srcdir: new File('src/main/java/'), destdir: destDir, includeAntRuntime: false) {
            schema(file: "application.xsd", namespace: "http://documentation.bonitasoft.com/application-xml-schema/1.0")
            classpath { pathelement(path: configurations.schemagen.asPath) }
            include(name: 'org/bonitasoft/engine/business/application/xml/**/*.java')
            exclude(name: '**/*Builder.java')
        }
    }
    inputs.dir('src/main/java/')
    outputs.file("$destDir/application.xsd")
    outputs.cacheIf { true }
}
tasks.named('processResources', ProcessResources.class).configure {
    from "$buildDir/schemas"
    dependsOn(schemagen)
}

test { jvmArgs "-javaagent:${classpath.find { it.name.contains("jmockit") }.absolutePath}" }
