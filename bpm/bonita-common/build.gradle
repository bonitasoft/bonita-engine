import org.bonitasoft.engine.gradle.PomUtils

configurations { schemagen }

dependencies {
    api platform("com.fasterxml.jackson:jackson-bom:${Deps.jacksonBomVersion}")
    api platform("org.bonitasoft.engine:bonita-artifacts-model-dependencies:${Deps.bonitaArtifactsModelVersion}")

    api "commons-io:commons-io:${Deps.commonsIOVersion}"
    api "com.fasterxml.jackson.core:jackson-databind"
    api "org.slf4j:slf4j-api:${Deps.slf4jVersion}"
    api "org.apache.commons:commons-lang3:${Deps.commonsLangVersion}"
    api "com.fasterxml.jackson.core:jackson-annotations"
    api("javax.xml.bind:jaxb-api") {
        exclude(group: "javax.activation") //replaced by jakarta
    }
    api("org.glassfish.jaxb:jaxb-runtime") {
        exclude(group: "javax.activation") //replaced by jakarta
    }
    api "org.bonitasoft.engine:bonita-business-archive"
    runtimeOnly "com.sun.activation:jakarta.activation"
    testImplementation "junit:junit:${Deps.junit4Version}"
    testImplementation "org.assertj:assertj-core:${Deps.assertjVersion}"
    testImplementation "org.mockito:mockito-core:${Deps.mockitoVersion}"
    testImplementation "ch.qos.logback:logback-classic:${Deps.logbackVersion}"
    testImplementation 'org.jmockit:jmockit:1.44'
    testImplementation "net.javacrumbs.json-unit:json-unit-fluent:${Deps.jsonUnitVersion}"
    testImplementation "xmlunit:xmlunit:${Deps.xmlunitVersion}"
    annotationProcessor "org.projectlombok:lombok:${Deps.lombokVersion}"
    compileOnly "org.projectlombok:lombok:${Deps.lombokVersion}"
    compileOnly project(':bpm:bonita-sap-jco-connector-api')
    testImplementation project(':bpm:bonita-sap-jco-connector-api')
    testImplementation project(':bpm:bonita-util:bonita-common-util-test')

    schemagen("org.glassfish.jaxb:jaxb-jxc:${Deps.jaxbJxcVersion}", "org.apache.commons:commons-lang3:${Deps.commonsLangVersion}", "org.slf4j:slf4j-api:${Deps.slf4jVersion}")
}

def generateInfoProperties = tasks.register("generateInfoProperties") {
    doFirst {
        project.file("build/generated/main/resources").mkdirs()
        project.file("build/generated/main/resources/info.properties").text = "version $project.version"
    }
    inputs.property "version", project.version
    outputs.file project.file("build/generated/main/resources/info.properties")
}

sourceSets {
    main {
        resources {
            srcDir project.file("build/generated/main/resources")
        }
    }
}
tasks.register("testsJar", Jar) {
    archiveClassifier = 'tests'
    from(sourceSets.test.output)
}

tasks.register("sourcesJar", Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

tasks.register("javadocJar", Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from project.components.java
            artifact project.sourcesJar
            artifact project.javadocJar
            artifact project.testsJar
            pom { pom ->
                name = "Bonita Common"
                description = "Bonita Common is the useful layer common to bonita-client and bonita-server"
                PomUtils.pomCommunityPublication(pom)
            }
        }
    }
}

def schemagen = tasks.register('schemagen') {
    def destDir = file("$buildDir/schemas")
    doLast {
        destDir.mkdirs()
        ant.taskdef(name: 'schemagen', classname: 'com.sun.tools.jxc.SchemaGenTask', classpath: configurations.schemagen.asPath)
     
        ant.schemagen(srcdir: new File('src/main/java/'), destdir: destDir, includeAntRuntime: false) {
            schema(file: "application.xsd", namespace: "http://documentation.bonitasoft.com/application-xml-schema/1.0")
            classpath { pathelement(path: configurations.schemagen.asPath) }
            include(name: 'org/bonitasoft/engine/business/application/xml/**/*.java')
            exclude(name: '**/*Builder.java')
        }
  
        ant.schemagen(srcdir: new File('src/main/java/'), destdir: destDir, includeAntRuntime: false) {
            schema(file: "bom.xsd", namespace: "http://documentation.bonitasoft.com/bdm-xml-schema/1.0")
            classpath { pathelement(path: configurations.schemagen.asPath) }
            include(name: 'org/bonitasoft/engine/bdm/model/**/*.java')
            exclude(name: 'org/bonitasoft/engine/bdm/model/QueryParameterTypes.java')
        }

        ant.schemagen(srcdir: new File('src/main/java/'), destdir: destDir, includeAntRuntime: false) {
            schema(file: "profiles.xsd", namespace: "http://documentation.bonitasoft.com/profile-xml-schema/1.0")
            classpath { pathelement(path: configurations.schemagen.asPath) }
            include(name: 'org/bonitasoft/engine/profile/xml/**/*.java')
        }
    }
    inputs.dir('src/main/java/')
    outputs.file("$destDir/application.xsd")
    outputs.file("$destDir/bom.xsd")
    outputs.file("$destDir/profiles.xsd")
    outputs.cacheIf { true }
}
tasks.named('processResources', ProcessResources.class).configure {
    from "$buildDir/schemas"
    dependsOn(generateInfoProperties, schemagen)
}

test { jvmArgs "-javaagent:${classpath.find { it.name.contains("jmockit") }.absolutePath}" }
