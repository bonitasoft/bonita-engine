import org.apache.tools.ant.filters.ReplaceTokens

group = 'org.bonitasoft.console'
description = 'Bonita Web Server'

repositories {
    maven { url = uri('https://maven.restlet.talend.com') }
}

dependencies {
    implementation(project(":bpm:bonita-common"))
    implementation(project(":bpm:bonita-web-extensions"))
    implementation libs.commonsIO
    implementation libs.slf4jApi
    implementation libs.ehCache
    implementation libs.commonsFileUpload
    implementation libs.commonsBeanUtils
    implementation libs.commonsCollections
    implementation libs.commonsLang
    implementation "com.googlecode.json-simple:json-simple:${Deps.jsonSimpleVersion}"
    implementation "org.tuckey:urlrewritefilter:${Deps.urlrewriteVersion}"
    implementation "org.glassfish.web:jakarta.servlet.jsp.jstl:${Deps.jakartaJstlVersion}"
    implementation "jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api:${Deps.jakartaJstlApiVersion}"
    implementation "org.restlet.jse:org.restlet:${Deps.restletVersion}"
    implementation("org.restlet.jee:org.restlet.ext.servlet:${Deps.restletVersion}") {
        exclude(group: "org.restlet.jee", module: "org.restlet")
    }
    implementation("org.restlet.jse:org.restlet.ext.jackson:${Deps.restletVersion}") {
        exclude(group: "org.codehaus.woodstox", module: "woodstox-core-asl")
        exclude(group: "org.codehaus.woodstox", module: "stax2-api")
        exclude(group: "org.yaml", module: "snakeyaml")
    }
    implementation "com.fasterxml.woodstox:woodstox-core:${Deps.woodstoxCoreVersion}"
    implementation libs.groovyCore
    implementation "org.apache.xbean:xbean-classloader:${Deps.xbeanClassloaderVersion}"
    implementation libs.springCore
    implementation libs.springContext
    implementation libs.springWeb
    implementation libs.springWebMvc
    implementation "org.fedorahosted.tennera:jgettext:${Deps.jgettextVersion}"
    implementation "com.googlecode.owasp-java-html-sanitizer:owasp-java-html-sanitizer:${Deps.owaspHTMLSanitizerVersion}"

    compileOnly libs.lombok
    annotationProcessor libs.lombok

    testImplementation(project(":bpm:bonita-server"))
    testImplementation "net.javacrumbs.json-unit:json-unit:${Deps.jsonUnitVersion}"
    testImplementation "org.assertj:assertj-core:${Deps.assertjVersion}"
    testImplementation libs.logback
    testImplementation libs.springTest
    testImplementation "com.github.stefanbirkner:system-rules:${Deps.systemRulesVersion}"
    testImplementation "org.mockito:mockito-core:${Deps.mockitoVersion}"
    testImplementation "org.hamcrest:hamcrest:${Deps.hamcrestVersion}"
    testImplementation "junit:junit:${Deps.junit4Version}"
    testImplementation libs.junit5api
    testImplementation "org.mockito:mockito-junit-jupiter:${Deps.mockitoVersion}"
    testRuntimeOnly libs.junitJupiterEngine
    testRuntimeOnly libs.junitVintageEngine
}

configurations {
    configureEach {
        // Specify woodstox version to override the version pulled by jackson-dataformat-xml (transitive dep of restlet)
        resolutionStrategy.force "com.fasterxml.woodstox:woodstox-core:${Deps.woodstoxCoreVersion}"
        // Specify Guava version to override the version pulled by owasp-java-html-sanitizer
        resolutionStrategy.force libs.guava //forcing managed version (transitive dependency of owasp-java-html-sanitizer)
    }
    tests
}

processResources {
    inputs.property("version", project.version)
    inputs.property("brandingVersion", brandingVersion)
    from('src/main/resources') {
        include '**/*'
        filter(ReplaceTokens, tokens: [projectVersion : project.version,
                                       brandingVersion: brandingVersion,
                                       buildYear      : String.valueOf(LocalDate.now().getYear())])
    }
}

tasks.register('buildZip', Zip) {
    from('src/main/resources') {
        include 'VERSION'
        filter(ReplaceTokens, tokens: [projectVersion : project.version,
                                       brandingVersion: brandingVersion,
                                       buildYear      : String.valueOf(LocalDate.now().getYear())])
    }
    from 'src/main/webapp'
}

tasks.named("build") {
    dependsOn tasks.named("buildZip")
}

tasks.register('testsJar', Jar) {
    dependsOn testClasses
    archiveClassifier = 'tests'
    from(sourceSets.test.output)
}

test {
    useJUnitPlatform { includeEngines 'junit-jupiter', 'junit-vintage' }
    dependsOn tasks.named("testsJar")
}

artifacts {
    tests testsJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact testsJar
            artifact buildZip
            pom { pom ->
                name = 'Bonita Web Server'
                description = 'Web API implementation module'
                org.bonitasoft.engine.gradle.PomUtils.pomCommunityPublication(pom)
            }
        }
    }
}
