plugins {
    id("bonita-http-test")
    id("bonita-docker-database")
    id("bonita-tests")
}
dependencies {
    api project(':bonita-test-api')
    api libs.commonsIO
    api project(':bpm:bonita-common')
    api project(':bpm:bonita-client')
    api project(':bonita-integration-tests:bonita-test-utils')
    api libs.assertj
    api libs.jsonUnitFluent
    api libs.systemRules
    api libs.concurrentUnit
    runtimeOnly libs.logback
    testImplementation project(':bpm:bonita-server')
    testImplementation testFixtures(project(':bpm:bonita-common'))
    testImplementation libs.awaitility
    testImplementation libs.xmlunit
    testImplementation libs.mockitoCore
}

test.doFirst {
    def emptyFile = new File("$project.buildDir/bonita_home_client_HTTP/engine-client/conf/bonita-client-custom.properties")
    emptyFile.getParentFile().mkdirs()
    emptyFile.text = ""
    copy {
        from file("$projectDir/bonita-client-http.properties")
        into file("$project.buildDir/bonita_home_client_HTTP/engine-client/work")
        rename 'bonita-client-http.properties', 'bonita-client-community.properties'
    }
    def invalidFile = new File("$project.buildDir/bonita_home_client_invalidAPIType/engine-client/conf/bonita-client-custom.properties")
    invalidFile.getParentFile().mkdirs()
    invalidFile.text = ""
    copy {
        from file("$projectDir/bonita-client-invalid.properties")
        into file("$project.buildDir/bonita_home_client_invalidAPIType/engine-client/work")
        rename 'bonita-client-invalid.properties', 'bonita-client-community.properties'
    }
}
group = 'org.bonitasoft.engine.test'
publishing {
    publications {
        mavenJava(MavenPublication) { from project.components.java }
    }
}

httpTests {
    integrationTestsSuite = "**/*IT.class"
}

tests {
    slowTestsSuite = "**/*IT.class"
}

databaseIntegrationTest { include "**/*IT.class" }