import org.bonitasoft.engine.gradle.PomUtils

plugins {
    id 'maven-publish'
}

dependencies {
    api project(':bpm:bonita-common')
    api "org.javassist:javassist:${Deps.javassistVersion}"
    compileOnly project(':bpm:bonita-client')
    testImplementation project(':bpm:bonita-client')
    testImplementation "junit:junit:${Deps.junit4Version}"
    testImplementation "org.mockito:mockito-core:${Deps.mockitoVersion}"
    testImplementation "org.assertj:assertj-core:${Deps.assertjVersion}"
    testImplementation("org.hibernate:hibernate-core:${Deps.hibernateVersion}") {
        exclude(module: 'jboss-transaction-api_1.2_spec')
    }
    testImplementation "org.projectlombok:lombok:${Deps.lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${Deps.lombokVersion}"
}

group = 'org.bonitasoft.engine.data'

tasks.register("sourcesJar", Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

tasks.register("javadocJar", Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from project.components.java
            artifact project.sourcesJar
            artifact project.javadocJar
            pom { pom ->
                name = "Bonita Business Data Client Resources"
                description = "Bonita Business Data Client Resources is a library used in BDM client DAO implementation"
                PomUtils.pomCommunityPublication(pom)
            }
        }
    }
}