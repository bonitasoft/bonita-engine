import org.bonitasoft.engine.gradle.PomUtils

dependencies {
    api(libs.commonsBeanUtils) {
        exclude(module: 'commons-collections')
    }
    api libs.commonsIO
    api libs.jakartaActivation
    api libs.commonsLang
    api(libs.xstream)
    api libs.micrometerCore
    api libs.springContext
    api libs.springBootAutoconfigure
    api libs.slf4jApi
    testImplementation "org.assertj:assertj-core:${Deps.assertjVersion}"
    testImplementation "org.mockito:mockito-core:${Deps.mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${Deps.mockitoVersion}"
    testImplementation "com.github.stefanbirkner:system-rules:${Deps.systemRulesVersion}"
    testImplementation "com.github.stefanbirkner:system-lambda:${Deps.systemLambdaVersion}"
    testImplementation libs.logback

    annotationProcessor libs.lombok
    compileOnly libs.lombok
}

description = 'Bonita Engine Util Classes'

tasks.register("sourcesJar", Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

tasks.register("javadocJar", Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from project.components.java
            artifact project.sourcesJar
            artifact project.javadocJar
            pom { pom ->
                name = "Bonita Business Data Generator"
                description = 'Bonita Engine Util Classes'
                PomUtils.pomCommunityPublication(pom)
            }
        }
    }
}
