import org.apache.tools.ant.filters.ReplaceTokens
import org.bonitasoft.engine.gradle.PomUtils

plugins { id 'distribution' }

configurations {
    distributionZip
    inDistrib
}

dependencies {
    compile "org.apache.commons:commons-text:1.9"
    compile "org.slf4j:slf4j-api:${Deps.slf4jVersion}"
    compile(group: 'org.springframework.boot', name: 'spring-boot-starter', version: Deps.springBootVersion) {
        exclude(module: 'jul-to-slf4j')
        exclude(module: 'log4j-to-slf4j')
        exclude(module: 'snakeyaml')
    }
    compile "org.yaml:snakeyaml:${Deps.snakeyamlVersion}"
    compile "org.springframework.boot:spring-boot-starter-jdbc:${Deps.springBootVersion}"
    compile "org.postgresql:postgresql:${Deps.postgresqlVersion}"
    compile ("mysql:mysql-connector-java:${Deps.mysqlVersion}") {
        exclude(module: 'protobuf-java')
    }
    compile "com.microsoft.sqlserver:mssql-jdbc:${Deps.mssqlVersion}"
    compile "com.h2database:h2:${Deps.h2Version}"
    compile("com.oracle.database.jdbc:ojdbc8:${Deps.oracleVersion}") {
        exclude(module: "ons")
        exclude(module: "oraclepki")
        exclude(module: "osdt_cert")
        exclude(module: "osdt_core")
        exclude(module: "ucp")
        exclude(module: "simplefan")
    }
    compile "commons-cli:commons-cli:${Deps.commonsCLIVersion}"
    compile(project(":platform:platform-resources"))
    runtime "ch.qos.logback:logback-classic:${Deps.logbackVersion}"
    testCompile "junit:junit:${Deps.junit4Version}"
    testCompile "org.assertj:assertj-core:${Deps.assertjVersion}"
    testCompile "org.mockito:mockito-core:${Deps.mockitoVersion}"
    testCompile "com.github.stefanbirkner:system-rules:${Deps.systemRulesVersion}"
    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: Deps.springBootVersion) {
        exclude(module: 'json-path')
    }
    testCompile "xmlunit:xmlunit:${Deps.xmlunitVersion}"
    testCompile project(':platform:platform-setup-test')

    inDistrib project(path: ':platform:platform-resources', configuration: 'distributionZip')
}

group = 'org.bonitasoft.platform'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from project.components.java
            artifact distZip
            pom { pom ->
                name = "Bonita Platform Setup"
                description = "Bonita Platform Setup is the standalone tool to setup a new Bonita platform"
                PomUtils.pomCommunityPublication(pom)
            }
        }
    }
}

processResources {
    from('src/main/resources') {
        include '*'
        filter(ReplaceTokens, tokens: [version: project.version])
    }
}

distTar.enabled = false
distributions {
    main {
        baseName = "Bonita-platform-setup"
        contents {
            includeEmptyDirs = false
            into('/') {
                from('src/main/standalone')
                include('*.sh')
                include('*.bat')
                fileMode = 0740 // the first 0 is important
            }
            into('/') {
                from('src/main/standalone')
                exclude('*.sh')
                exclude('*.bat')
            }
            into('/lib') {
                from jar
                from project.configurations.runtime {
                    exclude(module: 'jul-to-slf4j')
                    exclude(module: 'log4j-over-slf4j')
                    exclude(module: 'commons-logging')
                    exclude(module: 'log4j-api')
                    exclude(module: 'log4j-to-slf4j')
                }
            }
            configurations.inDistrib.resolvedConfiguration.resolvedArtifacts.each {
                from(zipTree(it.file)) {
                    include '**/sql/**'
                    eachFile { fcp ->
                        fcp.path = fcp.path.replaceAll(".*/sql/", "/platform_conf/sql/")
                    }
                }
            }
            configurations.inDistrib.resolvedConfiguration.resolvedArtifacts.each {
                from(zipTree(it.file)) {
                    exclude '**/sql/**'
                    exclude '**/org/**'
                    eachFile { fcp ->
                        fcp.path = fcp.path.replaceAll(".*${version}/", "/platform_conf/initial/")
                    }
                }
            }
            fileMode = 0640
        }
    }
}


tasks.distZip.dependsOn configurations.inDistrib

artifacts { distributionZip distZip }

test { include '**/*Test.class' }


def iT = tasks.getByName("integrationTest")
iT.configure {
    def testDir = new File(buildDir, "integrationTest")
    doFirst {
        testDir.mkdirs()
        systemProperty "bonita.distribution.path", distZip.outputs.files.first()
    }
    workingDir testDir
}
tasks.integrationTest.dependsOn distZip
