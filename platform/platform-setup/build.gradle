import org.apache.tools.ant.filters.ReplaceTokens
import org.bonitasoft.engine.gradle.PomUtils

plugins {
    id 'distribution'
    id 'bonita-tests'
}

configurations {
    distributionZip
    inDistrib
}

dependencies {
    api libs.commonsText
    api libs.slf4jApi
    api(libs.springBootStarter) {
        exclude(module: 'jul-to-slf4j')
        exclude(module: 'log4j-to-slf4j')
        exclude(module: 'snakeyaml')
    }
    api libs.snakeyaml
    api libs.springBootStarterJdbc
    api libs.postgresql
    api(libs.mysql) {
        exclude(module: 'protobuf-java')
    }
    api libs.msSqlServer
    api libs.h2
    api(libs.oracle) {
        exclude(module: "ons")
        exclude(module: "oraclepki")
        exclude(module: "osdt_cert")
        exclude(module: "osdt_core")
        exclude(module: "ucp")
        exclude(module: "simplefan")
    }
    api libs.commonsCLI
    annotationProcessor libs.lombok
    compileOnly libs.lombok
    api(project(":platform:platform-resources"))
    runtimeOnly libs.logback
    testImplementation libs.junit4
    testImplementation libs.assertj
    testImplementation libs.mockitoCore
    testImplementation libs.systemRules
    testImplementation(libs.springBootStarterTest) {
        exclude(module: 'json-path')
    }
    testImplementation libs.xmlunit
    testImplementation project(':platform:platform-setup-test')

    inDistrib project(path: ':platform:platform-resources', configuration: 'distributionZip')
}

group = 'org.bonitasoft.platform'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from project.components.java
            artifact distZip
            pom { pom ->
                name = "Bonita Platform Setup"
                description = "Bonita Platform Setup is the standalone tool to setup a new Bonita platform"
                PomUtils.pomCommunityPublication(pom)
            }
        }
    }
}

processResources {
    from('src/main/resources') {
        include '*'
        filter(ReplaceTokens, tokens: [version: project.version])
    }
}

distTar.enabled = false
distributions {
    main {
        distributionBaseName = "Bonita-platform-setup"
        contents {
            includeEmptyDirs = false
            into('/') {
                from('src/main/standalone')
                include('*.sh')
                include('*.bat')
                fileMode = 0740 // the first 0 is important
            }
            into('/') {
                from('src/main/standalone')
                exclude('*.sh')
                exclude('*.bat')
            }
            into('/lib') {
                from jar
                from project.configurations.runtimeClasspath {
                    exclude(module: 'jul-to-slf4j')
                    exclude(module: 'log4j-over-slf4j')
                    exclude(module: 'commons-logging')
                    exclude(module: 'log4j-api')
                    exclude(module: 'log4j-to-slf4j')
                }
            }
            configurations.inDistrib.resolvedConfiguration.resolvedArtifacts.each {
                from(zipTree(it.file)) {
                    include '**/sql/**'
                    eachFile { fcp ->
                        fcp.path = fcp.path.replaceAll(".*/sql/", "/platform_conf/sql/")
                    }
                }
            }
            configurations.inDistrib.resolvedConfiguration.resolvedArtifacts.each {
                from(zipTree(it.file)) {
                    exclude '**/sql/**'
                    exclude '**/org/**'
                    eachFile { fcp ->
                        fcp.path = fcp.path.replaceAll(".*${version}/", "/platform_conf/initial/")
                    }
                }
            }
            fileMode = 0640
        }
    }
}


tasks.distZip.dependsOn configurations.inDistrib

artifacts { distributionZip distZip }

test { include '**/*Test.class' }


def iT = tasks.getByName("integrationTest")
iT.configure {
    def testDir = new File(buildDir, "integrationTest")
    doFirst {
        testDir.mkdirs()
        systemProperty "bonita.distribution.path", distZip.outputs.files.first()
    }
    workingDir testDir
}
tasks.integrationTest.dependsOn distZip
