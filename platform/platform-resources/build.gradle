import org.apache.tools.ant.filters.PrefixLines
import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'distribution'

group = 'org.bonitasoft.platform'
description = ''

dependencies {
    annotationProcessor "org.projectlombok:lombok:${Deps.lombokVersion}"
    compileOnly "org.projectlombok:lombok:${Deps.lombokVersion}"
    compile "org.springframework:spring-tx:${Deps.springVersion}"
    compile "org.springframework:spring-jdbc:${Deps.springVersion}"
    compile "org.springframework:spring-context:${Deps.springVersion}"
    compile "org.slf4j:slf4j-api:${Deps.slf4jVersion}"
    compile "commons-io:commons-io:${Deps.commonsIOVersion}"

    testCompile "junit:junit:${Deps.junit4Version}"
    testCompile "org.assertj:assertj-core:${Deps.assertjVersion}"
    testCompile "org.mockito:mockito-core:${Deps.mockitoVersion}"
    testCompile "com.github.stefanbirkner:system-rules:${Deps.systemRulesVersion}"
}

processResources {
    from('src/main/resources') {
        include 'PLATFORM_ENGINE_VERSION'
        filter(ReplaceTokens, tokens: [version: project.version])
    }
    from("${projectDir}/../../bpm/bonita-core/bonita-process-engine/src/main/resources/bonita-platform-community.properties") {
        filter(PrefixLines, prefix: "#")
        rename 'bonita-platform-community.properties', 'platform_engine/bonita-platform-community-custom.properties'
    }
    from("${projectDir}/../../bpm/bonita-core/bonita-process-engine/src/main/resources/bonita-tenant-community.properties") {
        filter(PrefixLines, prefix: "#")
        rename 'bonita-tenant-community.properties', 'tenant_template_engine/bonita-tenant-community-custom.properties'
    }
}

configurations { distributionZip }

distTar.enabled = false
distributions {
    main {
        contents { from sourceSets.main.output }
    }
}

artifacts { distributionZip distZip }

publishing {
    publications {
        mavenJava(MavenPublication) {
            from project.components.java
            artifact distZip
        }
    }
}
