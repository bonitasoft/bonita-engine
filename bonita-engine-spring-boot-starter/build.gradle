import org.bonitasoft.engine.gradle.PomUtils

plugins { id "java-library" }

dependencies {
    api(project(":engine:bpm:bonita-common"))
    api(project(":engine:bpm:bonita-client"))

    api("org.springframework.boot:spring-boot-starter:${Deps.springBootVersion}")

    implementation(project(":engine:bonita-engine-standalone"))
    implementation(project(":engine:bpm:bonita-server"))

    annotationProcessor("org.springframework.boot:spring-boot-configuration-processor:${Deps.springBootVersion}")

    testImplementation("org.springframework.boot:spring-boot-test:${Deps.springBootVersion}")
    testImplementation("junit:junit:${Deps.junit4Version}")
    testImplementation("org.assertj:assertj-core:${Deps.assertjVersion}")
    testImplementation("org.awaitility:awaitility:${Deps.awaitilityVersion}")
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            from project.components.java
            artifact project.sourcesJar
            artifact project.javadocJar
            pom { pom ->
                name = "bonita-engine-spring-boot-starter"
                description = "bonita-engine-spring-boot-starter"
                PomUtils.pomCommunityPublication(pom)
            }
        }
    }
}

integrationTest.doFirst {
    systemProperty("org.bonitasoft.h2.database.dir", "./build/h2databasedir")
}
