import org.bonitasoft.engine.gradle.PomUtils

plugins {
    id 'java-library'
    id 'bonita-tests'
}

dependencies {
    api(project(":bpm:bonita-common"))
    api(project(":bpm:bonita-client"))

    api(libs.springBootStarter) {
        exclude(module: 'snakeyaml')
    }

    implementation(project(":bonita-engine-standalone"))
    implementation(project(":bpm:bonita-server"))

    annotationProcessor(libs.springBootConfigurationProcessor)

    testImplementation(libs.springBootTest)
    testImplementation(libs.assertj)
}

tasks.register("sourcesJar", Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

tasks.register("javadocJar", Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from project.components.java
            artifact project.sourcesJar
            artifact project.javadocJar
            pom { pom ->
                name = "bonita-engine-spring-boot-starter"
                description = "bonita-engine-spring-boot-starter"
                PomUtils.pomCommunityPublication(pom)
            }
        }
    }
}
