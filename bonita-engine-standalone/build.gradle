import org.bonitasoft.engine.gradle.PomUtils

plugins {
    id "java-library"
}

dependencies {

    api(project(":bpm:bonita-client"))

    implementation(project(":bpm:bonita-server"))
    implementation(project(':platform:platform-resources'))

    implementation libs.springContext
    implementation libs.narayanaJta
    implementation libs.tomcatDbcp
    implementation libs.slf4jApi
    runtimeOnly libs.h2


    annotationProcessor libs.lombok
    compileOnly libs.lombok

    testImplementation libs.assertj
    testImplementation libs.systemRules

    // These 3 drivers are used for some specific tests:
    testImplementation(libs.postgresql)
    testImplementation(libs.mysql)
    testImplementation(libs.h2)

}

tasks.register("sourcesJar", Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

tasks.register("javadocJar", Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from project.components.java
            artifact project.sourcesJar
            artifact project.javadocJar
            pom { pom ->
                name = "Bonita Engine Standalone"
                description = "Bonita Engine Standalone is the library to easily embed Bonita Engine is your applications"
                PomUtils.pomCommunityPublication(pom)
            }
        }
    }
}
